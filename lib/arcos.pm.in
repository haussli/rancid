package arcos;
##
## @PACKAGE@ @VERSION@
@copyright@
#
#  RANCID - Really Awesome New Cisco confIg Differ
#
#  arcos.pm - Arrcus Arcos rancid procedures

use 5.010;
use strict 'vars';
use warnings;
no warnings 'uninitialized';
require(Exporter);
our @ISA = qw(Exporter);

use rancid @VERSION@;

@ISA = qw(Exporter rancid main);
#XXX @Exporter::EXPORT = qw($VERSION @commandtable %commands @commands);

# load-time initialization
sub import {
    0;
}

# post-open(collection file) initialization
sub init {
    # add content lines and separators
    ProcessHistory("","","","!RANCID-CONTENT-TYPE: $devtype\n!\n");
    ProcessHistory("COMMENTS","keysort","B0","!\n");
    ProcessHistory("COMMENTS","keysort","C0","!\n");

    0;
}

# main loop of input of device output
sub inloop {
    my($INPUT, $OUTPUT) = @_;
    my($cmd, $rval);

TOP: while(<$INPUT>) {
	tr/\015//d;
	if (/^Error:/) {
	    print STDOUT ("$host clogin error: $_");
	    print STDERR ("$host clogin error: $_") if ($debug);
	    $clean_run = 0;
	    last;
	}
	while (/[#]\s*($cmds_regexp)\s*$/) {
	    $cmd = $1;
	    if (!defined($prompt)) {
		$prompt = ($_ =~ /^([^#>]+[#>])/)[0];
		$prompt =~ s/([][}{)(+\\])/\\$1/g;
		print STDERR ("PROMPT MATCH: $prompt\n") if ($debug);
	    }
	    print STDERR ("HIT COMMAND:$_") if ($debug);
	    if (! defined($commands{$cmd})) {
		print STDERR "$host: found unexpected command - \"$cmd\"\n";
		$clean_run = 0;
		last TOP;
	    }
	    if (! defined(&{$commands{$cmd}})) {
		printf(STDERR "$host: undefined function - \"%s\"\n",
		       $commands{$cmd});
		$clean_run = 0;
		last TOP;
	    }
	    $rval = &{$commands{$cmd}}($INPUT, $OUTPUT, $cmd);
	    delete($commands{$cmd});
	    if ($rval == -1) {
		$clean_run = 0;
		last TOP;
	    }
	}
	if (/[#]\s?quit$/) {
	    $clean_run = 1;
	    last;
	}
    }
}

# This routine parses "show version"
sub ShowVersion {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In ShowVersion: $_" if ($debug);

    while (<$INPUT>) {
	tr/\015//d;
	next if (/^(\s*|\s*$cmd\s*)$/);
	last if (/^$prompt/);
	next if (/^\s+\^$/);
	return(1) if (/Line has invalid autocommand /);
	return(1) if (/(invalid (input|command) detected|type help or )/i);
	return(-1) if (/command authorization failed/i);

	# drop trailing spaces
	s/  *$//;

	/version product-name "([^\"]*)"/ &&
	    ProcessHistory("COMMENTS","keysort","A1",
			   "!Chassis type: $1\n");
	/version serial-num (\S+)/ &&
	    ProcessHistory("COMMENTS","keysort","D9",
			   "!Serial Number: $1\n");
	/version total-memory "([^\"]*)"/ &&
	    ProcessHistory("COMMENTS","keysort","B1", "!Memory: $1\n");

	ProcessHistory("COMMENTS","keysort","VERSION","! $_");
    }
    ProcessHistory("COMMENTS","keysort","VERSION","!\n");
    return(0);
}

# This routine parses "show hardware"
sub ShowHardware {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In ShowVersion: $_" if ($debug);

    while (<$INPUT>) {
	tr/\015//d;
	next if (/^(\s*|\s*$cmd\s*)$/);
	last if (/^$prompt/);
	next if (/^\s+\^$/);
	return(1) if (/Line has invalid autocommand /);
	return(1) if (/(invalid (input|command) detected|type help or )/i);
	return(-1) if (/command authorization failed/i);
	return(-1) if (/error: application communication failure/i);

	# skip allocated, free, watermarks & their timestamps
	next if (/(estimated-free|objects-allocated|high-watermark|raw-(free|used))/);
	# skip feature tables
	if (/\s*feature$/) {
	    while (<$INPUT>) {
		tr/\015//d;
		next if (/^(\s*|\s*$cmd\s*)$/);
		last if (/^$prompt/);
		last if (/^\s*$/);
	    }
	}

	# drop indexer counts
	next if (/^\s+used\s+\d+/);
	next if (/^\s+hold-count\s+\d+/);

	# XXX for now, drop the per-unit information.  Appears to be mostly
	# useless.
	last if (/^UNIT/);

	ProcessHistory("","","","! $_");
    }
    ProcessHistory("","","","!\n");
    return(0);
}

# This routine processes a "show running-config"
sub WriteTerm {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In WriteTerm: $_" if ($debug);
    my($comment, $linecnt);

    while (<$INPUT>) {
TOP:
	tr/\015//d;
	last if (/^$prompt/);
	return(1) if (!$linecnt && /^\s+\^\s*$/);
	next if (/^\s*$cmd\s*$/);
	return(1) if (/Line has invalid autocommand /);
	next if (/^\s+\^\s*$/);
	return(1) if (/(invalid (input|command) detected|type help or )/i);
	return(1) if (/\%Error: No such file or directory/);
	return(1) if (/(Open device \S+ failed|Error opening \S+:)/);
	return(0) if ($found_end);		# Only do this routine once
	return(-1) if (/command authorization failed/i);
	return(-1) if (/error: application communication failure/i);
	return(-1) if (/% ?configuration buffer full/i);

	/^! no configuration change since last restart/i && next;

	# skip emtpy lines at the beginning
	if (!$linecnt && /^\s*$/) {
	    next;
	}
	/Non-Volatile memory is in use/ && return(-1); # NvRAM is locked
	/% Configuration buffer full, / && return(-1); # buffer is in use
	$linecnt++;

	# skip the crap
	if (/^(##+|(building|current) configuration)/i) {
	    while (<$INPUT>) {
		next if (/^Current configuration\s*:/i);
		next if (/^:/);
		next if (/^([%!].*|\s*)$/);
		next if (/^ip add.*ipv4:/);	# band-aid for 3620 12.0S
		last;
	    }
	    tr/\015//d;
	}
	# config timestamp on MDS/NX-OS
	/Time: / && next;

	# skip consecutive comment lines to avoid oscillating extra comment
	# line on some access servers.  grrr.
	if (/^!\s*$/) {
	    next if ($comment);
	    ProcessHistory("","","",$_);
	    $comment++;
	    next;
	}
	$comment = 0;

	# prune bgp passwords, possibly others
	if (/^(\s+auth-password\s+)(\S+)/ && $filter_pwds >= 1) {
	    ProcessHistory("","","","!$1<removed>$'"); next;
	}

	# XXX prune snmp communities
	#if (/^(snmp-server .*community) ([08] )?(\S+)/) {
	#    if ($filter_commstr) {
	#	    ProcessHistory("SNMPSERVERCOMM","keysort","$_", "!$1 <removed>$'") && next;
	#	} else {
	#	    ProcessHistory("SNMPSERVERCOMM","keysort","$_","$_") && next;
	#    }
	#}   
	# prune tacacs/radius server keys
	if (/^(\s+(tacacs|radius) secret-key\s)(\S+)/ && $filter_pwds >= 1) {
	    ProcessHistory("","","","!$1<removed>$'"); next;
	}

	# catch anything that wasnt matched above.
	ProcessHistory("","","","$_");
    }
    # arcos lacks a definitive "end of config" marker.  if we have seen at
    # least 5 lines of input, we can be reasonably sure that we have the config.
    if ($linecnt > 5) {
	$found_end = 1;
    }

    return(0);
}

1;
