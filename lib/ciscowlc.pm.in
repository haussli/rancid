package ciscowlc;
##
## @PACKAGE@ @VERSION@
@copyright@
#
#  RANCID - Really Awesome New Cisco confIg Differ
#
#  ciscowlc.pm - Cisco WLC V4.X
#
#  based on modified version of ciscoips by Josh Yost on 4/16/2008
use 5.010;
use strict 'vars';
use warnings;
no warnings 'uninitialized';
require(Exporter);
our @ISA = qw(Exporter);

use rancid @VERSION@;

@ISA = qw(Exporter rancid main);
#XXX @Exporter::EXPORT = qw($VERSION @commandtable %commands @commands);

# load-time initialization
sub import {
    0;
}

# post-open(collection file) initialization
sub init {
    # add content lines and separators
    ProcessHistory("","","","!RANCID-CONTENT-TYPE: $devtype\n!\n");

    0;
}

# main loop of input of device output
sub inloop {
    my($INPUT, $OUTPUT) = @_;
    my($cmd, $rval);

TOP: while(<$INPUT>) {
	tr/\015//d;
	if (/^Error:/) {
	    print STDOUT ("$host wlogin error: $_");
	    print STDERR ("$host wlogin error: $_") if ($debug);
	    $clean_run = 0;
	    last;
	}
	while (/($cmds_regexp)/) {
	    $cmd = $1;
	    if (!defined($prompt)) {
		$prompt = ($_ =~ /^([^#]+>)/)[0];
		$prompt =~ s/([][}{)(\\])/\\$1/g;
		print STDERR ("PROMPT MATCH: $prompt\n") if ($debug);
	    }
	    print STDERR ("HIT COMMAND:$_") if ($debug);
	    if (! defined($commands{$cmd})) {
		print STDERR "$host: found unexpected command - \"$cmd\"\n";
		$clean_run = 0;
		last TOP;
	    }
	    if (! defined(&{$commands{$cmd}})) {
		printf(STDERR "$host: undefined function - \"%s\"\n",
		       $commands{$cmd});
		$clean_run = 0;
		last TOP;
	    }
	    $rval = &{$commands{$cmd}}($INPUT, $OUTPUT, $cmd);
	    delete($commands{$cmd});
	    if ($rval == -1) {
		$clean_run = 0;
		last TOP;
	    }
	}
	if (/^.*logout(\s*connection.*closed.*)?$/i) {
	    $clean_run = 1;
	    last;
	}
    }
}

# This routine parses "show running-config"
sub ShowConfig {
    my($INPUT, $OUTPUT, $cmd) = @_;
    my($linecnt) = 0;
    print STDERR "    In ShowConfig: $_" if ($debug);
    ProcessHistory("","","","\n!--WLC Begin Config Data--!\n");

    while (<$INPUT>) {
	tr/\001//d;				# filter garbage char
	tr/\015//d;
	tr/\020//d;
	last if (/^$prompt/);

	next if (/^\s*rogue ap classify/);
	next if (/^\s*rogue (adhoc|client) (alert|unknown)/i);
	next if (/^\s*interface nat-address management set -?[0-9]{4,}\./);
	next if (/\x{7f}/);			# filter garbage line
	
	$linecnt++;

	# These can not be imported, so comment them
	if (/^\s*cisco public safety is not allowed to set in this domain/i ||
	    /^\s*outdoor mesh ext.unii b domain channels:/i ||
	    /^\s*service port.*/i ||
	    /^\s*wlan express setup.*/i ||
	    /^\s*wmm-ac (disabled|enabled)/i) {
	    ProcessHistory("","","","!$_"); next;
	}

	# remove snmp community string data
	if (/^(\s*snmp community create)/ && $filter_pwds >= 1) {
	    ProcessHistory("","","","!$1 <removed>\n"); next;
	}
	if (/^(\s*snmp community accessmode (ro|rw))/ && $filter_pwds >= 1) {
	    ProcessHistory("","","","!$1 <removed>\n"); next;
	}
	if (/^(\s*snmp community ipaddr\s\S+\s\S+) / && $filter_pwds >= 1) {
	    ProcessHistory("","","","!$1 <removed>\n"); next;
	}

	next if (/^(\s*|\s*$cmd\s*)$/);

	$linecnt++;
	if (! /^$prompt/) {
	    ProcessHistory("","","","$_");
	}
    }
    ProcessHistory("","","","\n!--WLC End Config Data--!\n");
    # WLC lacks a definitive "end of config" marker.
    if ($linecnt > 5) {
	$found_end = 1;
	return(1);
    }
    return(0);
}

# This routine parses "show sysinfo"
sub ShowSysinfo {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In ShowSysinfo: $_" if ($debug);
    ProcessHistory("","","","\n!WLC Show Sysinfo Start\n!\n");

    while (<$INPUT>) {
	tr/\015//d;

	my($skipprocess) = 0;

	if (/^System Up Time/) { $skipprocess = 1; }
	if (/^fan status/i) { $skipprocess = 1; }
	if (/^Number of Active Clients/) { $skipprocess = 1; }
	if (/^OUI Classification Failure Count/) { $skipprocess = 1; }
	if (/^(memory|cpu) (current|average)/i) { $skipprocess = 1; }
	if (/^(mgig|internal|external) temperature/i) { $skipprocess=1; }
	if (/rpm$/) { $skipprocess = 1; }

	last if (/^$prompt/);
	next if (/^(\s*|\s*$cmd\s*)$/);
	if (! /^$prompt/) {
		if (! $skipprocess) {
			ProcessHistory("","","","! $_");
		}
	}
    }
    ProcessHistory("","","","!\n!WLC Show Sysinfo End\n");
    return(0);
}

# This routine parses "show udi"
sub ShowUdi {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In ShowUdi: $_" if ($debug);
    ProcessHistory("","","","\n!WLC Show Udi Start\n!\n");

    while (<$INPUT>) {
	tr/\015//d;

	last if (/^$prompt/);
	next if (/^(\s*|\s*$cmd\s*)$/);
	ProcessHistory("","","","! $_");
    }
    ProcessHistory("","","","!\n!WLC Show Udi End\n");
    return(0);
}

# This routine parses "show ap summary"
sub ShowApSummary {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In ShowApSummary $_" if ($debug);
    ProcessHistory("","","","\n!WLC Show Ap Summary Start\n!\n");

    my @aplist = ();

    while (<$INPUT>) {
        tr/\015//d;

        # Table of APs: columns are separated by 2 or more spaces. Print only the
        # first 8 columns. Depending on the controller version, column 9 may contain
        # the number of associated clients to that AP, which changes all the time.
        # This is why we ignore columns 9 and following.
        # Dont print line, but add it to an array so we can sort the list of APs later.
        if (/^(.*?(?=  +)(?:  +.*?){7})/) {
            push (@aplist, $1);
            next;
        }

        # loop control
        last if (/^$prompt/);
        next if (/^(\s*|\s*$cmd\s*)$/);

        # print everything that is not the table
        ProcessHistory("","","","! $_");
    }

    # print table headers (2 lines), then the sorted table
    my $header1 = shift @aplist;
    ProcessHistory("","","","! $header1\n");
    my $header2 = shift @aplist;
    ProcessHistory("","","","! $header2\n");
    foreach (sort @aplist) {
        ProcessHistory("","","","! $_\n");
    }

    ProcessHistory("","","","!\n!WLC Show Ap Summary End\n");
    return(0);
}

# This routine parses "show advanced 802.11b summary"
sub ShowAdvancedSummary {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In ShowAdvancedSummary $_" if ($debug);
    ProcessHistory("","","","\n!WLC Show Advanced 802.11b Summary Start\n!\n");

    while (<$INPUT>) {
        tr/\015//d;

        # loop control
        last if (/^$prompt/);
        next if (/^(\s*|\s*$cmd\s*)$/);
        next if (/\* global assignment|Member RRM Information/);

        # Table of APs: columns are separated by 1 or more spaces. Print only the
        # first 2 columns.
        # Treat the first lines differnt (table header).
        # Sort the rest of the lines alphabetically (table content).
        if (/^(((AP Name|MAC Address) +){2})/) {
            ProcessHistory("","","","! $1\n");
            next;
        }
        if (/^(.*?(?= +)(?: +.*?){2})/) {
            ProcessHistory("APs","valsort","$1","! $1\n");
            next;
        }

        # print everything that wasn't matched before
        ProcessHistory("","","","!!! $_");
    }

    ProcessHistory("","","","!\n!WLC Show Advanced 802.11b Summary End\n");
    return(0);
}

# This routine parses "show ap inventory all"
sub ShowApInventory {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In ShowApInventory $_" if ($debug);
    ProcessHistory("","","","\n!WLC Show Ap Inventory Start\n!\n");

    my @temp = ();
    while (<$INPUT>) {
        tr/\015//d;

        # loop control
        last if (/^$prompt/);
        next if (/^(\s*|\s*$cmd\s*)$/);
        next if (/^NAME:.*DESCR:/);

        push (@temp, $_);
    }

    # combine 2 lines into one, store into new array @aplist
    my @aplist = ();
    my $i=0;
    my $line = "";
    foreach (@temp) {
      my $s = $_;
      if ($i++ % 2 == 0) {
        $s =~ s/\n/\t/;
        $s =~ s/Inventory for //;
        $line = $line.$s;
      } else {
        $line = $line.$s;
        push (@aplist,$line);
        $line = "";
      }
    }

    foreach (sort @aplist) {
      ProcessHistory("","","","! $_");
    }

    ProcessHistory("","","","!\n!WLC Show Ap Inventory End\n");
    return(0);
}

# This routine parses all "show" commands without modifying the output
sub Show {
    my($INPUT, $OUTPUT, $cmd) = @_;
    print STDERR "    In Show $_" if ($debug);
    ProcessHistory("","","","\n!WLC \"$cmd\" Start\n!\n");

    while (<$INPUT>) {
        tr/\015//d;

        last if (/^$prompt/);
        next if (/^(\s*|\s*$cmd\s*)$/);
        ProcessHistory("","","","! $_");
    }
    ProcessHistory("","","","!\n!WLC \"$cmd\" End\n");
    return(0);
}

1;
